group 'org.jetbrains.dokka'
version dokka_version
import proguard.gradle.ProGuardTask

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'net.sf.proguard:proguard-gradle:5.2.1'
    }
}

apply plugin: 'java'
apply plugin: 'maven-publish'

task wrapper(type: Wrapper) {
    gradleVersion = '2.5'
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

task sourceJar(type: Jar) {
    from fileTree('../src')
}

task proguard(type: ProGuardTask) {
    configuration 'dokka.pro'

    injars  'build/libs/dokka-fatjar-${dokka_version}.jar'
    outjars 'build/libs/dokka-fatjar-${dokka_version}.out.jar'
}

jar {
    manifest {
        attributes "Implementation-Title": "Dokka Kotlin Documentation tool"
        attributes "Implementation-Version": version
        attributes "Main-Class" : "org.jetbrains.dokka.MainKt"
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    fileTree(project.file('../lib')).filter {
        it.name.endsWith('.jar') &&
                !it.name.contains("-sources") &&
                !it.name.contains("-javadoc")
    }.each {
        from (zipTree(it)) {
            exclude 'META-INF/MANIFEST.MF'
            exclude 'META-INF/*.SF'
            exclude 'META-INF/*.DSA'
            exclude 'META-INF/*.RSA'
            exclude '**/*.kt'
            exclude '**/*.java'
            exclude '**/*.md'
        }
    }
    from (zipTree(project.file('../out/dokka.jar'))) {
        exclude 'META-INF/MANIFEST.MF'
        exclude 'META-INF/*.SF'
        exclude 'META-INF/*.DSA'
        exclude 'META-INF/*.RSA'
        exclude '**/*.kt'
        exclude '**/*.java'
        exclude '**/*.md'
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}
