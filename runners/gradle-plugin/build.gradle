apply plugin: 'java'
apply plugin: 'kotlin'

sourceCompatibility = 1.8

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: "com.gradle.plugin-publish"

configurations {
    provided
}

tasks.withType(AbstractCompile) {
    classpath += configurations.provided
    classpath += configurations.shadow
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'

    shadow group: 'org.jetbrains.kotlin', name: 'kotlin-runtime', version: kotlin_for_gradle_version
    shadow group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: kotlin_for_gradle_version

    compile project(":integration")

    provided gradleApi()
    provided localGroovy()
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
}

processResources {
    eachFile {
        if (it.name == "org.jetbrains.dokka.properties") {
            it.filter { line ->
                line.replace("<version>", dokka_version)
            }
        }
    }
}

shadowJar {
    baseName = 'dokka-gradle-plugin'
    classifier = ''
}

apply plugin: 'maven-publish'

publishing {
    publications {
        dokkaGradlePlugin(MavenPublication) {
            from components.shadow
            artifactId = 'dokka-gradle-plugin'

            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

bintrayPublication(project, ['dokkaGradlePlugin'])

configurations.archives.artifacts.clear()
artifacts {
    archives shadowJar
}

pluginBundle {
    website = 'http://www.kotlinlang.org/'
    vcsUrl = 'https://github.com/kotlin/dokka.git'
    description = 'Dokka, the Kotlin documentation tool'
    tags = ['dokka', 'kotlin', 'kdoc']

    plugins {
        dokkaGradlePlugin {
            id = 'org.jetbrains.dokka'
            displayName = 'Dokka plugin'
        }
    }

    mavenCoordinates {
        groupId = "org.jetbrains.dokka"
        artifactId = "dokka-gradle-plugin"
    }
}
buildscript {
    ext.kotlin_version = '1.1.2-5'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}
repositories {
    mavenCentral()
}