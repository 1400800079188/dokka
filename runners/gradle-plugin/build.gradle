apply plugin: 'java'

sourceCompatibility = 1.6

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: "com.gradle.plugin-publish"

configurations {
    provided
}

tasks.withType(AbstractCompile) {
    classpath += configurations.provided
    classpath += configurations.shadow
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'

    compile group: 'org.jetbrains.kotlin', name: 'kotlin-runtime', version: kotlin_version
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: kotlin_version

    compile project(":integration")

    provided gradleApi()
    provided localGroovy()
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
}

processResources {
    eachFile {
        if (it.name == "org.jetbrains.dokka.properties") {
            it.filter { line ->
                line.replace("<version>", dokka_version)
            }
        }
    }
}

shadowJar {
    baseName = 'dokka-gradle-plugin'
    classifier = ''
}

apply plugin: 'maven-publish'

publishing {
    publications {
        dokkaGradlePlugin(MavenPublication) {
            from components.shadow
            artifactId = 'dokka-gradle-plugin'

            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

bintrayPublication(project, ['dokkaGradlePlugin'])


pluginBundle {
    website = 'http://www.kotlinlang.org/'
    vcsUrl = 'https://github.com/kotlin/dokka.git'
    description = 'Dokka, the Kotlin documentation tool'
    tags = ['dokka', 'kotlin']

    plugins {
        dokkaGradlePlugin {
            id = 'org.jetbrains.dokka'
            displayName = 'Dokka plugin'
        }
    }
}