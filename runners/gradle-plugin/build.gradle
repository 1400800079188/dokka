apply plugin: 'java'

sourceCompatibility = 1.6

apply plugin: 'com.github.johnrengelman.shadow'

configurations {
    provided
}

tasks.withType(AbstractCompile) {
    classpath += configurations.provided
    classpath += configurations.shadow
}

dependencies {

    shadow project(':runners:fatjar')
    testCompile group: 'junit', name: 'junit', version: '4.12'

    provided group: 'org.jetbrains.kotlin', name: 'kotlin-runtime', version: kotlin_version
    provided group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: kotlin_version
    provided gradleApi()
    provided localGroovy()
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.5'
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

shadowJar {
    baseName = 'dokka-gradle-plugin'
    classifier = ''
    relocate('kotlin.', 'dokkakotlin.')
}

apply plugin: 'maven-publish'

publishing {
    publications {
        dokkaGradlePlugin(MavenPublication) {
            from components.shadow
            artifactId = 'dokka-gradle-plugin'

            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

bintrayPublication(project, ['dokkaGradlePlugin'])

