apply plugin: 'java'

sourceCompatibility = 1.6

task wrapper(type: Wrapper) {
    gradleVersion = '2.5'
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

apply plugin: 'com.github.johnrengelman.shadow'

configurations {
    provided
}

tasks.withType(AbstractCompile) {
    classpath += configurations.provided
    classpath += configurations.shadow
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    shadow project(':runners:fatjar')
    shadow project(':runners:gradle-plugin')

    provided gradleApi()
    provided localGroovy()

    shadow 'com.android.tools.build:gradle:2.0.0'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
}

shadowJar {
    baseName = 'dokka-android-gradle-plugin'
    classifier = ''
    relocate('kotlin.', 'dokkakotlin.')
}

apply plugin: 'maven-publish'

publishing {
    publications {
        dokkaAndroidGradlePlugin(MavenPublication) {
            from components.shadow
            artifactId = 'dokka-android-gradle-plugin'

            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

bintrayPublication(project, ['dokkaAndroidGradlePlugin'])